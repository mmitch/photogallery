#!/usr/bin/perl
#
# converts photogallery changes to HTML page
#
# 2007-2008 (c) by Christian Garbs <mitch@cgarbs.de>
# licensed under GNU GPL v2
#

use strict;
use warnings;

# parse configuration file
my %conf;
eval(`cat ~/.photogallery-conf.pl`);

# check variables
foreach (qw( DATADIR DIRPREFIX WEBPREFIX RSSURL CHARSET DATELANG )) {
    die "configuration key $_ is missing\n" unless exists $conf{$_};
}

# get old diffs
my @diff;
my $diffs = "$conf{DATADIR}/last_diffs";

open DIFFS, '<', $diffs or die "can't open `$diffs': $!";
while (my $line = <DIFFS>) {
    chomp $line;
    if ($line =~ /^(\d+)\t(\d+)\t(\d+)\t(.+)$/) {
	push @diff, {
	    'OLD' => $1,
	    'NEW' => $2,
	    'DATE' => $3,
	    'PATH' => $4
	};
    }
}
close DIFFS or die "can't close `$diffs': $!";

# print header
print <<"EOF";
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml1.dtd">
<html><head>
<title>photogallery updates</title>
<meta http-equiv="content-type" content="text/html; charset=${conf{CHARSET}}" />
<link rel="alternate" type="application/rss+xml" title="RSS-Feed" href="$conf{RSSURL}">
</head><body>
<h1>photogallery updates</h1>
EOF
    ;

# print changes
print "<ul>\n";
foreach my $diff (reverse @diff) {
    my $date = $diff->{DATE};
    $date =~ s/^(\d\d\d\d)(\d\d)(\d\d)(\d\d)(\d\d).*$/$3.$2.$1 $4:$5h/;
    my $path = $diff->{PATH};
    $path =~ s/^$conf{DIRPREFIX}//;
    my $url = $conf{WEBPREFIX}.$path;
    $path =~ s:/index.html$::;

    my $text;

    if ($diff->{'OLD'} == 0) {
	$text = "neuer Ordner mit $diff->{'NEW'} Bildern";
    } elsif ($diff->{'NEW'} == 0) {
	# gelöscht, so what?
    } elsif ($diff->{'NEW'} > $diff->{'OLD'}) {
	$text = ($diff->{'NEW'} - $diff->{'OLD'}) . " neue Bilder";
    } else {
	$text = ($diff->{'OLD'} - $diff->{'NEW'}) . " Bilder gelöscht";
    }

    printf "<li>%s <a href=\"%s\">%s</a><br />%s<br />&nbsp;</li>\n",
    $date,
    $url,
    $path,
    $text
	if defined $text;
}
print "</ul>\n";

# print footer
my $date = `LANG=${conf{DATELANG}} date`;
chomp $date;
print "<p>(<a href=\"$conf{RSSURL}\">RSS feed</a>)</p>\n";
print "<p><small><small><i>generated on $date by ";
print '$Id: photogallery_diff2html.pl,v 1.7 2007-08-12 17:29:21 mitch Exp $';
print "</i></small></small></p></body></html>\n";
